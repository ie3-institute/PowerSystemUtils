plugins {
	id 'groovy' // groovy support
	id 'java' // java support
	id 'maven-publish'
	id 'signing'
	id 'pmd' //code check, working on source code
	id 'com.github.spotbugs' version '4.7.2' //code check, working on byte code
	id 'com.diffplug.spotless' version '5.12.5'//code format
	id 'com.simonharrer.modernizer' version '2.1.0-1' //detect deprecated APIs
	id 'com.github.onslip.gradle-one-jar' version '1.0.6' // pack a self contained jar
	id 'jacoco' // java code coverage plugin
	id "org.sonarqube" version "3.3" // sonarqube
}

ext {
	slf4jVersion = '1.7.31'
	javaVersion = JavaVersion.VERSION_1_8

	scriptsLocation = 'gradle' + File.separator + 'scripts' + File.separator //location of script plugins
}

group = 'com.github.ie3-institute'
version = '1.6-SNAPSHOT'
description = 'PowerSystemUtils'
sourceCompatibility = javaVersion
targetCompatibility = javaVersion

apply from: scriptsLocation + 'tests.gradle'
apply from: scriptsLocation + 'pmd.gradle'
apply from: scriptsLocation + 'spotbugs.gradle'
apply from: scriptsLocation + 'spotless.gradle'
apply from: scriptsLocation + 'modernizer.gradle'
apply from: scriptsLocation + 'checkJavaVersion.gradle'
apply from: scriptsLocation + 'selfContainedJar.gradle'
apply from: scriptsLocation + 'jacoco.gradle' // jacoco java code coverage
apply from: scriptsLocation + 'mavenCentralPublish.gradle'


repositories {
	mavenCentral()
	maven { url = "https://www.jitpack.io" } // allows for github repos as dependencies
}

dependencies {
	constraints {
		implementation( 'junit:junit:4.13.2+' ){
			because "CVE-2020-15250 - Temporary folder vulnerability - https://github.com/advisories/GHSA-269g-pwp5-87pp"
		}
	}

	// logging
	compile "org.slf4j:slf4j-api:$slf4jVersion"  // slf4j wrapper
	compile 'com.lmax:disruptor:3.4.4' // async logging
	compile 'org.apache.logging.log4j:log4j-api:2.14.1' // log4j
	compile 'org.apache.logging.log4j:log4j-core:2.14.1' // log4j
	compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.14.1' // log4j -> slf4j

	compile 'javax.measure:unit-api:2.1.3' // quantities
	compile 'tech.units:indriya:2.1.2'  // quantities
	compile 'com.google.code.gson:gson:2.8.7' // java serialization, POJOs -> JSON
	compile 'org.apache.commons:commons-lang3:3.12.0' // for HashCodeBuilder
	compile 'com.github.johanneshiry:OSMonaut:v1.1.1' // pbf parser
	compile 'org.apache.commons:commons-math3:3.6.1' // mathematics and statistics components
	// JTS Topology Suite for GeoPositions, License: EPL 1.0 / EDL 1.0
	compile 'org.locationtech.jts:jts-core:1.18.1'
	compile 'org.locationtech.jts.io:jts-io-common:1.18.1'

	compile 'commons-io:commons-io:2.11.0' // I/O functionalities
	compile 'org.apache.commons:commons-compress:1.20' // I/O functionalities

	// testing
	testCompile 'org.spockframework:spock-core:1.3-groovy-2.5'

	spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.11.0' // find bugs in java programs
}

wrapper {
	gradleVersion = '6.8.3'
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

task printVersion {
	doLast {
		println project.version
	}
}
