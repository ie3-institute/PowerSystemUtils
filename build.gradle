plugins {
	id 'groovy' // groovy support
	id 'java' // java support
	id 'scala' // scala support
	id 'maven-publish'
	id 'signing'
	id 'pmd' //code check, working on source code
	id 'com.github.spotbugs' version '6.0.7' //code check, working on byte code
	id 'com.diffplug.spotless' version '6.25.0'//code format
	id 'jacoco' // java code coverage plugin
	id "org.sonarqube" version "4.4.1.3373" // sonarqube
	id "org.scoverage" version "8.0.3" // scala code coverage scoverage
	id "com.github.maiflai.scalatest" version "0.32" // run scalatest without specific spec task
}

ext {
	javaVersion = JavaVersion.VERSION_17
	scalaVersion = '2.13'
	scalaBinaryVersion = '2.13.12'
	scriptsLocation = 'gradle' + File.separator + 'scripts' + File.separator //location of script plugins
}

group = 'com.github.ie3-institute'
version = '3.0-SNAPSHOT'
description = 'PowerSystemUtils'

java {
	sourceCompatibility = javaVersion
	targetCompatibility = javaVersion
}

apply from: scriptsLocation + 'pmd.gradle'
apply from: scriptsLocation + 'spotbugs.gradle'
apply from: scriptsLocation + 'spotless.gradle'
apply from: scriptsLocation + 'checkJavaVersion.gradle'
apply from: scriptsLocation + 'test.gradle'
apply from: scriptsLocation + 'jacoco.gradle' // jacoco java code coverage
apply from: scriptsLocation + 'mavenCentralPublish.gradle'
apply from: scriptsLocation + 'sonarqube.gradle'
apply from: scriptsLocation + 'scoverage.gradle' // scoverage scala code coverage

repositories {
	mavenCentral()
}

dependencies {
	constraints {
		implementation( 'org.jsoup:jsoup:1.17.2' ){
			because "CVE-2022-36033 6.1 Improper Neutralization of Input During Web Page Generation ('Cross-site Scripting') vulnerability pending CVSS allocation"
		}
	}

	// logging
	implementation 'org.slf4j:slf4j-api:2.0.11'  // slf4j wrapper
	implementation 'com.lmax:disruptor:4.0.0' // async logging
	implementation platform('org.apache.logging.log4j:log4j-bom:2.22.1')
	implementation 'org.apache.logging.log4j:log4j-api' // log4j
	implementation 'org.apache.logging.log4j:log4j-core' // log4j
	implementation 'org.apache.logging.log4j:log4j-slf4j-impl' // log4j -> slf4j

	implementation "com.typesafe.scala-logging:scala-logging_$scalaVersion:3.9.5"

	implementation 'javax.measure:unit-api:2.2' // quantities
	implementation 'tech.units:indriya:2.2'  // quantities
	implementation 'com.google.code.gson:gson:2.10.1' // java serialization, POJOs -> JSON
	implementation 'org.apache.commons:commons-lang3:3.14.0' // for HashCodeBuilder
	implementation 'org.apache.commons:commons-math3:3.6.1' // mathematics and statistics components
	// JTS Topology Suite for GeoPositions, License: EPL 1.0 / EDL 1.0
	implementation 'org.locationtech.jts:jts-core:1.19.0'
	implementation 'org.locationtech.jts.io:jts-io-common:1.19.0'

	// Used in EmpiricalRandom.java, could be removed
	implementation 'org.eclipse.collections:eclipse-collections:11.1.0'

	implementation 'commons-io:commons-io:2.15.1' // I/O functionalities
	implementation 'org.apache.commons:commons-compress:1.25.0' // I/O functionalities

	// testing
	testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'

	/* --- Scala libs --- */
	// CORE Scala //
	implementation "org.scala-lang:scala-library:${scalaBinaryVersion}"
	implementation "org.scala-lang.modules:scala-parallel-collections_${scalaVersion}:1.0.4"
	implementation "org.typelevel:cats-core_${scalaVersion}:2.10.0" // advanced functional programming

	// TEST Scala //
	implementation "org.scalatest:scalatest_$scalaVersion:3.2.17"
	testRuntimeOnly 'com.vladsch.flexmark:flexmark-all:0.64.8'
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}


task printVersion {
	doLast {
		println project.version
	}
}
