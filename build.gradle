plugins {
	id 'groovy' // groovy support
	id 'java' // java support
	id 'scala' // scala support
	id 'maven-publish'
	id 'signing'
	id 'pmd' //code check, working on source code
	id 'com.github.spotbugs' version '5.0.6' //code check, working on byte code
	id 'com.diffplug.spotless' version '6.4.2'//code format
	id 'jacoco' // java code coverage plugin
	id "org.sonarqube" version "3.3" // sonarqube
	id "com.github.maiflai.scalatest" version "0.32" // run scalatest without specific spec task
}

ext {
	javaVersion = JavaVersion.VERSION_17
	scalaVersion = '3'
	scalaBinaryVersion = '3.1.2'
	scriptsLocation = 'gradle' + File.separator + 'scripts' + File.separator //location of script plugins
}

group = 'com.github.ie3-institute'
version = '2.0-SNAPSHOT'
description = 'PowerSystemUtils'
sourceCompatibility = javaVersion
targetCompatibility = javaVersion

apply from: scriptsLocation + 'pmd.gradle'
apply from: scriptsLocation + 'spotbugs.gradle'
apply from: scriptsLocation + 'spotless.gradle'
apply from: scriptsLocation + 'checkJavaVersion.gradle'
apply from: scriptsLocation + 'test.gradle'
apply from: scriptsLocation + 'jacoco.gradle' // jacoco java code coverage
apply from: scriptsLocation + 'mavenCentralPublish.gradle'
apply from: scriptsLocation + 'sonarqube.gradle'

repositories {
	mavenCentral()
	maven { url = "https://www.jitpack.io" } // allows for github repos as dependencies
}

dependencies {
	constraints {
		implementation( 'junit:junit:4.13.2+' ){
			because "CVE-2020-15250 - Temporary folder vulnerability - https://github.com/advisories/GHSA-269g-pwp5-87pp"
		}
	}

	// logging
	implementation 'org.slf4j:slf4j-api:1.7.36'  // slf4j wrapper
	implementation 'com.lmax:disruptor:3.4.4' // async logging
	implementation platform('org.apache.logging.log4j:log4j-bom:2.17.2')
	implementation 'org.apache.logging.log4j:log4j-api' // log4j
	implementation 'org.apache.logging.log4j:log4j-core' // log4j
	implementation 'org.apache.logging.log4j:log4j-slf4j-impl' // log4j -> slf4j

	implementation "com.typesafe.scala-logging:scala-logging_$scalaVersion:3.9.4"

	implementation 'javax.measure:unit-api:2.1.3' // quantities
	implementation 'tech.units:indriya:2.1.3'  // quantities
	implementation 'com.google.code.gson:gson:2.9.0' // java serialization, POJOs -> JSON
	implementation 'org.apache.commons:commons-lang3:3.12.0' // for HashCodeBuilder
	implementation 'com.github.johanneshiry:OSMonaut:v1.1.1' // pbf parser
	implementation 'org.apache.commons:commons-math3:3.6.1' // mathematics and statistics components
	// JTS Topology Suite for GeoPositions, License: EPL 1.0 / EDL 1.0
	implementation 'org.locationtech.jts:jts-core:1.18.2'
	implementation 'org.locationtech.jts.io:jts-io-common:1.18.2'

	implementation 'commons-io:commons-io:2.11.0' // I/O functionalities
	implementation 'org.apache.commons:commons-compress:1.21' // I/O functionalities

	// testing
	testImplementation 'org.spockframework:spock-core:2.1-groovy-3.0'

	/* --- Scala libs --- */
	// CORE Scala //
	implementation "org.scala-lang:scala3-library_3:${scalaBinaryVersion}"
	implementation 'org.scala-lang.modules:scala-parallel-collections_3:1.0.4'
	implementation group: 'org.typelevel', name: 'cats-core_3', version: '2.7.0' // advanced functional programming

	// TEST Scala //
	implementation "org.scalatest:scalatest_$scalaVersion:3.2.11"
	testRuntimeClasspath 'com.vladsch.flexmark:flexmark-all:0.64.0'


}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}


task printVersion {
	doLast {
		println project.version
	}
}
