plugins {
	id "com.jfrog.artifactory" version "4.9.7"
	id 'groovy' // groovy support
	id 'java' // java support
	id 'maven-publish'
	id 'signing'
	id 'pmd' //code check, working on source code
	id 'com.github.spotbugs' version '2.0.0' //code check, working on byte code
	id 'com.diffplug.gradle.spotless' version '3.27.1'//code format
	id 'com.simonharrer.modernizer' version '1.6.0-1' //detect deprecated APIs
	id 'com.github.onslip.gradle-one-jar' version '1.0.5' // pack a self contained jar
	id 'jacoco' // java code coverage plugin
	id "org.sonarqube" version "2.7.1" // sonarqube
}

ext {
	// version (changing these should be considered thoroughly!)
	hibernateVersion = '5.3.3.Final'
	unitsOfMeasurementVersion = '1.0.10'
	slf4jVersion = '1.7.26'
	javaVersion = JavaVersion.VERSION_1_8

	scriptsLocation = 'gradle' + File.separator + 'scripts' + File.separator //location of script plugins
}

group = 'com.github.ie3-institute'
version = '1.3.2-SNAPSHOT'
description = 'PowerSystemUtils'
sourceCompatibility = javaVersion
targetCompatibility = javaVersion

apply from: scriptsLocation + 'tests.gradle'
apply from: scriptsLocation + 'pmd.gradle'
apply from: scriptsLocation + 'spotbugs.gradle'
apply from: scriptsLocation + 'spotless.gradle'
apply from: scriptsLocation + 'modernizer.gradle'
apply from: scriptsLocation + 'checkJavaVersion.gradle'
apply from: scriptsLocation + 'selfContainedJar.gradle'
apply from: scriptsLocation + 'jacoco.gradle' // jacoco java code coverage
apply from: scriptsLocation + 'mavenCentralPublish.gradle'


repositories {
	jcenter()
	maven { url = "https://www.jitpack.io" } // allows for github repos as dependencies
}

dependencies {
	// logging
	compile "org.slf4j:slf4j-api:$slf4jVersion"  // slf4j wrapper
	compile 'com.lmax:disruptor:+' // async logging
	compile 'org.apache.logging.log4j:log4j-api:+' // log4j
	compile 'org.apache.logging.log4j:log4j-core:+' // log4j
	compile 'org.apache.logging.log4j:log4j-slf4j-impl:+' // log4j -> slf4j

	compile 'javax.measure:unit-api:+' // quantities
	compile "tec.uom:uom-se:$unitsOfMeasurementVersion"  // quantities
	compile group: 'tech.units', name: 'indriya', version: '2.0'  // quantities
	compile 'com.google.code.gson:gson:+' // java serialization, POJOs -> JSON
	compile 'org.apache.commons:commons-lang3:+' // for HashCodeBuilder
	compile 'com.github.johanneshiry:OSMonaut:+' // pbf parser
	compile "org.hibernate:hibernate-spatial:$hibernateVersion" // spatial operations in GeoUtils
	compile 'org.apache.commons:commons-math3:+' // mathematics and statistics components
	// JTS Topology Suite for GeoPositions, License: EPL 1.0 / EDL 1.0
	compile 'org.locationtech.jts:jts-core:1.16.1'
	compile 'org.locationtech.jts.io:jts-io-common:1.16.1'

	// testing
	testCompile 'org.spockframework:spock-core:1.3-groovy-+'

	spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:+' // find bugs in java programs

}

wrapper {
	gradleVersion = '6.0.1'
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

task printVersion {
	doLast {
		println project.version
	}
}
